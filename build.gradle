apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

sourceSets.main.java.srcDirs = [ "src/" ]
sourceCompatibility = 1.7

ext {
    libVersion = '1.5'
    gdxVersion = '1.8.0'
    isSnapshot = '-SNAPSHOT'
}

group = "com.github.czyzby"
archivesBaseName = "gdx-lml"
version = "$libVersion.$gdxVersion$isSnapshot"

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

jar {
    from project.sourceSets.main.allSource
    from project.sourceSets.main.output
    baseName = 'gdx-lml'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.github.czyzby:gdx-kiwi:$libVersion.$gdxVersion$isSnapshot"
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }


      repository(url: getReleaseRepositoryUrl()) {
        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
      }

      snapshotRepository(url: getSnapshotRepositoryUrl()) {
        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
      }

      pom.project {
        name 'Gdx Lml'
        packaging 'jar'
        description 'Templates for LibGDX Scene2D UI with HTML-like syntax and FreeMarker-inspired macros.'
        url 'http://github.com/czyzby/gdx-lml'

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        
        scm {
          connection 'scm:git:git@github.com:czyzby/gdx-lml.git'
          developerConnection 'scm:git:git@github.com:czyzby/gdx-lml.git'
          url 'http://github.com/czyzby/gdx-lml/'
        }

        developers {
          developer {
            id 'mj'
            name 'MJ'
            email 'john.hervicc@gmail.com'
          }
        }
      }
    }
  }
}
